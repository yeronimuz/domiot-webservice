import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins{
   id "com.github.johnrengelman.shadow" version "7.1.2"
   id 'net.nemerosa.versioning' version '2.8.2'
   id 'jacoco'
   id 'java'
   id 'maven-publish'
}

version '0.0.1'

ext{
   baseName = 'lnb-iot-webservice'
   classifier = 'SNAPSHOT'
   mainClassName = 'com.lankheet.iot.webservice.WebService'
}

repositories{
   mavenLocal()
   mavenCentral()
}

publishing{
   publications{
      maven(MavenPublication){
         groupId 'com.lankheet.iot'
         artifactId 'lnb-webservice'
         version version

         from components.java
      }
   }
}

shadowJar{
   mergeServiceFiles()
   exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
   manifest{
      attributes(
         'Main-Class': project.ext.mainClassName,
         'Implementation-Title': 'Lankheet IOT WebService',
         'Implementation-Version': version,
         'Implementation-Classifier': project.ext.classifier,
         'Built-By': System.properties['user.name'],
         'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
            .format(DateTimeFormatter.ISO_ZONED_DATE_TIME),
         'Build-Revision': versioning.info.commit,
         'Created-By': "Gradle ${gradle.gradleVersion}",
         'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
         'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
      )
   }
}

jacoco{
   toolVersion = "0.8.7"
   reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test {
   finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
   dependsOn test // tests are required to run before generating the report
}

dependencies{
   implementation 'com.lankheet.iot:datatypes:0.5.1-SNAPSHOT'
   implementation 'com.fasterxml.jackson.core:jackson-core:2.13.3'
   implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
   implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.3'
   implementation group: 'org.hibernate', name: 'hibernate-core', version: '6.1.1.Final'
   implementation 'io.dropwizard:dropwizard-core:2.1.0'
   implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
   implementation group: 'mysql', name: 'mysql-connector-java', version: '5.1.44'

   testImplementation 'info.cukes:cucumber-java:1.2.6'
   testImplementation 'info.cukes:cucumber-junit:1.2.6'
   testImplementation group: 'org.hibernate', name: 'hibernate-core', version: '6.1.1.Final'
   testImplementation group: 'org.hibernate.common', name: 'hibernate-commons-annotations', version: '6.0.2.Final'
   testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.7.1'
   testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.2'
   testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.5.1'
   testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.5.1'
}
